
plugins {
    id 'java-library'
    id 'application'
}

group = 'kr.pe.kwonnam.research'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = '11'
targetCompatibility = '11'

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

ext {
    retrofit2Version = "2.9.0"
    lombokVersion = "1.18.12"
    junitVersion = "5.6.2"
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.slf4j:jcl-over-slf4j:1.7.30'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "com.squareup.retrofit2:retrofit:${retrofit2Version}"
    implementation "com.squareup.retrofit2:converter-jackson:${retrofit2Version}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.16.1"

}
configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging' // replaced with jcl-over-slf4j
    all*.exclude group: 'log4j', module: 'log4j' // replaced with log4j-over-slf4j
    all*.exclude group: 'org.apache.logging.log4j' // replaced with log4j-over-slf4j
    all*.exclude module: 'slf4j-log4j12'

    compileOnly {
        extendsFrom annotationProcessor
    }
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = 'kr.pe.kwonnam.research.java.App'
}